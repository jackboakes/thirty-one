cmake_minimum_required(VERSION 3.20...4.2)

project(thirty_one
        VERSION 0.1.0
        DESCRIPTION "Thirty One card game built with raylib"
        LANGUAGES CXX)

set(CMAKE_CXX_STANDARD 23)
set(CMAKE_CXX_STANDARD_REQUIRED True)
set(CMAKE_CXX_EXTENSIONS OFF)

include(FetchContent)
set(RAYLIB_VERSION 5.5)
find_package(raylib ${RAYLIB_VERSION} QUIET) 
if (NOT raylib_FOUND)
    set(BUILD_EXAMPLES OFF CACHE BOOL "" FORCE) # don't build the supplied examples
    set(BUILD_GAMES OFF CACHE BOOL "" FORCE) # don't build the supplied example games
    set(BUILD_TESTING OFF CACHE BOOL "" FORCE)
    FetchContent_Declare(
        raylib
        URL https://github.com/raysan5/raylib/archive/refs/tags/${RAYLIB_VERSION}.tar.gz
        DOWNLOAD_EXTRACT_TIMESTAMP OFF
    )
    FetchContent_MakeAvailable(raylib)
endif()

set(HEADERS
    include/application.h
    include/backgroundlayer.h
    include/card.h
    include/gamestate.h
    include/layer.h
    include/gameplaylayer.h
    include/globals.h
    include/zone.h
    include/deckzone.h
    include/handzone.h
    include/discardzone.h
)

set(SOURCES
    src/application.cpp
    src/backgroundlayer.cpp
    src/card.cpp
    src/main.cpp
    src/gameplaylayer.cpp
    src/deckzone.cpp
    src/handzone.cpp
    src/discardzone.cpp
)

add_executable(${PROJECT_NAME} ${SOURCES} ${HEADERS})

target_link_libraries(${PROJECT_NAME}
    PRIVATE raylib
)

target_include_directories(${PROJECT_NAME} PRIVATE include/)

if(MSVC)
    target_compile_options(${PROJECT_NAME} PRIVATE 
	/std:c++23preview 
	/permissive-)
    # Sets the Visual Studio startup project to our poject otherwise it will default to ALL_BUILD
    set_property(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR} PROPERTY VS_STARTUP_PROJECT ${PROJECT_NAME})
endif()